plugins {
    id 'com.android.library'
    id 'maven-publish'
    id "com.google.cloud.artifactregistry.gradle-plugin" version "2.2.1"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        versionCode 4
        versionName "3.2.1"

        minSdkVersion 16
        targetSdkVersion 30

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

allprojects {
    ext.repositoryURL = "artifactregistry://europe-west1-maven.pkg.dev/sep-infra/tmp-maven-registry"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

dependencies {
    compileOnly 'com.google.android.gms:play-services-ads:22.1.0'
    compileOnly 'com.huawei.hms:hms-ads-identifier:3.4.26.303'

    testImplementation 'com.google.android.gms:play-services-ads:22.1.0'
    testImplementation 'com.huawei.hms:hms-ads-identifier:3.4.26.303'

    //noinspection GradleDynamicVersion
    testImplementation 'junit:junit:4.13.+'
    //noinspection GradleDynamicVersion
    testImplementation 'androidx.test:core:1.5.0'
    //noinspection GradleDynamicVersion
    testImplementation 'org.robolectric:robolectric:4.5.+'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_ARTIFACT_VERSION

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = PUBLISH_ARTIFACT_DESCRIPTION
                    url = PUBLISH_ARTIFACT_URL
                    developers {
                        developer {
                            id = 'felix-ulmer'
                            name = 'Felix Ulmer'
                            email = 'felix.ulmer@andrena.de'
                        }
                    }
                }

                artifact(androidSourcesJar)
                artifact("$buildDir/outputs/aar/piano-analytics-release.aar")
            }
        }
        repositories {
            maven {
                name = "ATGithubPackages"
                url = "https://maven.pkg.github.com/digitalegarage/audiopilot-android"
                credentials {
                    username = project.findProperty('githubActor') ?: System.getenv("GITHUB_ACTOR")
                    password = project.findProperty('githubPublishToken') ?: System.getenv("GITHUB_PUBLISH_TOKEN")
                }
            }
            maven {
                name = "ATGcpPublicPackages"
                url = "gcs://tmp-maven-repo/maven/release"
            }
        }
    }
}



ext {
    PUBLISH_GROUP_ID = 'io.piano'
    PUBLISH_ARTIFACT_ID = 'analytics'
    PUBLISH_ARTIFACT_VERSION = '3.2.2'
    PUBLISH_ARTIFACT_DESCRIPTION = 'Piano Analytics SDK for Android'
    PUBLISH_ARTIFACT_URL = 'https://github.com/digitalegarage/audiopilot-android/'
}
